apply plugin: 'com.android.library'
apply plugin: 'com.google.protobuf'

apply from: 'version.gradle'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://maven.google.com' }
        maven { url "https://repo.eclipse.org/content/repositories/paho-snapshots/" }
    }

    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.6'
    }
}

android {
    compileSdkVersion 26

    publishNonDefault true

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        versionCode buildVersionCode()
        versionName buildVersionName()
        multiDexEnabled true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError true
        absolutePaths false

        lintConfig file("lint.xml")
        warning "MissingTranslation", "ExtraTranslation"

        // b/64679950
        warning "InvalidPackage"

        htmlOutput file("build/reports/whistlepunk-lint-results.html")
        xmlOutput file("build/reports/whistlepunk-lint-results.xml")
    }

    sourceSets {
        main {
            java {
                srcDir '../../third_party/jsyn'
                srcDir 'src/api/wireapi/java'
            }
            proto {
                srcDir 'src/main/proto'
            }
            aidl {
                srcDir 'src/api/wireapi/aidl'
            }
        }
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
            java.srcDir 'src/robolectricTest/java'
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.0.0-alpha-3'
    }
    generateProtoTasks {
        all()*.builtins {
            javanano {
                // Options added to --javanano_out
                option 'java_multiple_files=false'
                option 'ignore_services=true'
            }
        }
    }
}

dependencies {
    implementation project(':scalarinputapi')
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3.1'
    implementation 'com.android.support:support-annotations:26.0.1'
    implementation 'com.android.support:support-v4:26.0.1'
    implementation 'com.android.support:support-v13:26.0.1'
    implementation 'com.android.support:appcompat-v7:26.0.1'
    implementation 'com.android.support:design:26.0.1'
    implementation 'com.android.support:cardview-v7:26.0.1'
    implementation 'com.android.support:recyclerview-v7:26.0.1'
    implementation 'com.android.support:percent:26.0.1'
    implementation 'com.android.support:exifinterface:26.0.1'
    implementation 'com.github.bumptech.glide:glide:4.1.1'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.1.1'
    implementation 'com.google.guava:guava:20.0'
    implementation 'com.google.protobuf.nano:protobuf-javanano:3.0.0-alpha-3.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.3'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'joda-time:joda-time:2.9.4'
    implementation 'org.joda:joda-convert:1.8'
    implementation 'com.tbruyelle.rxpermissions2:rxpermissions:0.9.4@aar'

    // ========================================================================
    // added: these are for communication to the external database 'ThingsSpeak'
    //implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

    implementation 'org.eclipse.paho:org.eclipse.paho.client.mqttv3:1.1.0'
    implementation 'org.eclipse.paho:org.eclipse.paho.android.service:1.1.1'

    implementation 'com.squareup.okhttp3:okhttp:3.4.2'

    // added: this is for the number picker in the frequency Popup window
    implementation 'com.michaelmuenzer.android:ScrollableNumberPicker:0.2.2'
    // ========================================================================

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'
    testImplementation'junit:junit:4.12'
    androidTestImplementation('junit:junit:4.12') {
        exclude module: 'hamcrest-core'
    }
    annotationProcessor 'com.google.dagger:dagger-compiler:2.11'
    implementation 'com.google.dagger:dagger:2.11'
    compileOnly 'javax.annotation:jsr250-api:1.0'
    implementation 'com.bignerdranch.android:expandablerecyclerview:2.1.1'
    implementation 'com.squareup.okhttp3:okhttp:3.14.0'

    testImplementation('org.robolectric:robolectric:3.3.2') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation 'org.robolectric:shadows-core:3.3.2'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
}
